on 1:EXIT: {
  set -g %translationsLoaded $false
  set -g %caseTracker $false
}

on 1:TEXT:*:#fuelrats,#ratchat: {
  if ($nick isin "MechaSqueak[BOT]") {
    if ($regex(signal, $1-, /RATSIGNAL Case #(\d+) (?:PC|Xbox|Playstation)(?: \(Code Red\))?(?: \(Odyssey\))? – CMDR (.+) – System: ".+" \(.+\) – Language: .+ \(([a-z]{2})(?:-\w{2,3})?\)(?: – Nick: ([\w\[\]\^-{|}]+))? \((?:PC|XB|PS)_SIGNAL\)/S)) {
      if ($regml(signal, 4) == $null) {
        /set %nickname $regml(signal, 2)
      }
      else {
        /set %nickname $regml(signal, 4)
      }

      /set -g %caseTracker $true

      /set %caseNumber $regml(signal, 1)
      /set %language $regml(signal, 3)

      /echo -tsg new client %caseNumber => %nickname ( $+ %language $+ )

      /hadd -m10 cases %caseNumber %nickname
      /hadd -m10 lang %caseNumber %language
    }

    if ($regex(nickCMD, $1-, /Client nick for case #(\d+) \(.*\) has been changed to (.*)\./S)) {
      /set %nickname $regml(nickCMD, 2)
      /set %caseNumber $regml(nickCMD, 1)

      /echo -tsg nick change %caseNumber => %nickname
      /hadd -m10 cases %caseNumber %nickname
    }

    if ($regex(nickChange, $1-, /Caution: Client of case #(\d+) \(.*\) has changed IRC nick to (.*)/S)) {
      /set %nickname $regml(nickChange, 2)
      /set %caseNumber $regml(nickChange, 1)

      /echo -tsg nick change %caseNumber => %nickname
      /hadd -m10 cases %caseNumber %nickname
    }

    if ($regex(nickChangeJoin, $1-, /Caution: Case #(\d+) client \(.*\) has rejoined with a different name! \((.*)\)/S)) {
      /set %nickname $regml(nickChangeJoin, 2)
      /set %caseNumber $regml(nickChangeJoin, 1)

      /echo -tsg nick change %caseNumber => %nickname on join
      /hadd -m10 cases %caseNumber %nickname
    }

    if ($regex(langChange, $1-, /Language for case #(\d+) \(.*\) has now been changed to (\w{2}).*/S)) {
      /set %caseNumber $regml(langChange, 1)
      /set %language $regml(langChange, 2)

      /echo -tsg lang change %caseNumber => %language
      /hadd -m10 lang %caseNumber %language
    }

    if ($regex(close, $1-, /Successfully closed case #(\d+).*/S)) {
      /set %caseNumber $regml(close, 1)

      if ($hget(cases, %caseNumber) != $null) {
        /echo -tsg case %caseNumber closed
        /hdel cases %caseNumber
      }

      if ($hget(lang, %caseNumber) != $null) {
        /hdel lang %caseNumber
      }
    }

    if ($regex(md, $1-, /Successfully added case #(\d+) \(.*\) to the deletion list\./S)) {
      /set %caseNumber $regml(md, 1)

      if ($hget(cases, %caseNumber) != $null) {
        /echo -tsg case %caseNumber MDed
        /hdel cases %caseNumber
      }

      if ($hget(lang, %caseNumber) != $null) {
        /hdel lang %caseNumber
      }
    }
  }
}

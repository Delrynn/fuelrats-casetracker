; add case# clientname langcode
/addcase {
  hadd -m10 cases $$1 $$2
  hadd -m10 lang $$1 $$3
  set -g %caseTracker $true
  echo -ag added case $$1 => $$2 ( $+ $$3 $+ )
}

; delete case#
/delcase {
  if ($hget(cases, $$1) != $null) {
    hdel cases $$1
    echo -ag deleted case $$1
  }
  else {
    echo -ag case $$1 does not exist
  }
}

; get case#
/getcase {
  if ($hget(cases, $$1) != $null) {
    echo -ag $$1 => $hget(cases, $$1)
  }
  else {
    echo -ag case # $+ $$1 not found
  }
}

; delete all cases
/clearcases {
  hfree cases

  echo -ag all cases deleted
}

; list all cases
/listcases {
  var %i = 0
  while (%i < $hget(cases).size) {
    if ($hget(cases, %i) != $null) {
      echo -ag %i => $hget(cases, %i)
    }

    inc %i
  }
}

/findClientNames {
  set -n %clientNames
  set %found 0

  var %i = 1
  while (%i <= 3) {
    if ($eval($ $+ %i,2) isnum) {
      if ($hget(cases, $eval($ $+ %i,2)) != $null) {
        %found = 1
        %tokens = $hget(cases, $eval($ $+ %i,2))
        %nickname = $gettok(%tokens,1,36)

        %clientNames = %clientNames $+ $chr(32) $+ %nickname
      }
    }
    else {
      %clientNames = %clientNames $+ $chr(32) $+ $eval($ $+ %i,2)
    }

    inc %i
  }

  if (%found == 0) {
    set -n %clientNames $$1-
  }

  return %clientNames
}

/findOdy {
  if ($hget(cases, $$1) != $null) {
    %tokens = $hget(cases, $$1)
    %platform = $gettok(%tokens,2,36)
    %ody = $gettok(%tokens,3,36)

    if (%platform == PC) {
      return %ody
    }
    else {
      return Horizons
    }
  }
}

; $1 = regex name, $2 = groupNumber, $3 = tokenField
; %tokens = $addtok(%tokens,%nickname,36)
; %tokens = $addtok(%tokens,%platform,36)
; %tokens = $addtok(%tokens,%ody,36)
; %tokens = $addtok(%tokens,%language,36)
/changeTokenValue {
  /set %caseNumber $regml($$1, 1)
  /set %newValue $regml($$1, $$2)
  /set %tokenPos 0

  if ($$3 isin "nickname") {
    %tokenPos = 1
  }
  elseif ($$3 isin "platform") {
    %tokenPos = 2
  }
  elseif ($$3 isin "ody") {
    %tokenPos = 3

    ; if the "not " is missing then its odyssey
    if (%newValue == $null) {
      %newValue = Odyssey
    }
    else {
      %newValue = Horizons
    }
  }
  elseif ($$3 isin "language") {
    %tokenPos = 4
  }

  if (%tokenPos == 0) {
    echo -tsg tokenPos not found in changeTokenValue
    return 0
  }

  %tokens = $hget(cases, %caseNumber)
  %oldValue = $gettok(%tokens,%tokenPos,36)
  %tokens = $puttok(%tokens,%newValue,%tokenPos,36)

  /hadd -m10 cases %caseNumber %tokens
  /echo -tsg $$3 changed for case %caseNumber : %oldValue => %newValue
}

; /addcase case# clientName Platform leg/hor/ody Lang-Code
/addcase {
  ; change platform to what mecha uses
  if ($$3 == xb) {
    %platform = Xbox
  }
  elseif ($$3 == ps) {
    %platform = Playstation
  }
  else {
    %platform = $$3
  }

  if ($$4 == ody) {
    %ody = Odyssey
  }
  elseif ($$4 == hor) {
    %ody = Horizons
  }
  elseif ($$4 == leg) {
    %ody = Legacy
  }
  else {
    %ody = $$4
  }

  %tokens = ""
  %tokens = $addtok(,$$2,36)
  %tokens = $addtok(%tokens,%platform,36)
  %tokens = $addtok(%tokens,%ody,36)
  %tokens = $addtok(%tokens,$$5,36)

  set -g %caseTracker $true
  hadd -m10 cases $$1 %tokens
  echo -tsg new client $$1 => $gettok(%tokens,1,36) - speaking $gettok(%tokens,4,36) on $gettok(%tokens,2,36) ( $+ $gettok(%tokens,3,36) $+ )
}

; /delcase case#
/delcase {
  if ($hget(cases, $$1) != $null) {
    hdel cases $$1
    echo -ag deleted case $$1
  }
  else {
    echo -ag case $$1 does not exist
  }
}

; /getcase case#
/getcase {
  if ($hget(cases, $$1) != $null) {
    %tokens = $hget(cases, $$1)
    echo -ag $$1 => $gettok(%tokens,1,36) - speaking $gettok(%tokens,4,36) on $gettok(%tokens,2,36) ( $+ $gettok(%tokens,3,36) $+ )
  }
  else {
    echo -ag case # $+ $$1 not found
  }
}

; delete all cases
/clearcases {
  hfree cases

  echo -ag all cases deleted
}

; list all cases
/listcases {
  var %i = 0
  while (%i <= $hget(cases).size) {
    if ($hget(cases, %i) != $null) {
      %tokens = $hget(cases, %i)
      echo -ag %i => $gettok(%tokens,1,36) - speaking $gettok(%tokens,4,36) on $gettok(%tokens,2,36) ( $+ $gettok(%tokens,3,36) $+ )
    }

    inc %i
  }
}

/getClientNames {
  set -n %clientNames
  set %found 0

  var %i = 1
  while (%i <= 3) {
    if ($eval($ $+ %i,2) isnum) {
      if ($hget(cases, $eval($ $+ %i,2)) != $null) {
        %found = 1
        %tokens = $hget(cases, $eval($ $+ %i,2))
        %nickname = $gettok(%tokens,1,36)

        %clientNames = %clientNames $+ $chr(32) $+ %nickname
      }
    }
    else {
      %clientNames = %clientNames $+ $chr(32) $+ $eval($ $+ %i,2)
    }

    inc %i
  }

  if (%found == 0) {
    set -n %clientNames $$1-
  }

  return %clientNames
}

/getMode {
  if ($hget(cases, $$1) != $null) {
    %tokens = $hget(cases, $$1)
    return $gettok(%tokens,3,36)
  }
}

/getLanguage {
  if ($hget(cases, $$1) != $null) {
    %tokens = $hget(cases, $$1)
    return $gettok(%tokens,4,36)
  }
}

; $1 = caseNumber, $2 = tokenField, $3 = newValue
; %tokens = $addtok(%tokens,%nickname,36)
; %tokens = $addtok(%tokens,%platform,36)
; %tokens = $addtok(%tokens,%mode,36)
; %tokens = $addtok(%tokens,%language,36)
/changeTokenValue {
  /set %caseNumber $$1
  /set %newValue $$3-
  /set %tokenPos 0

  if ($$2 isin "nickname") {
    %tokenPos = 1
  }
  elseif ($$2 isin "platform") {
    %tokenPos = 2
  }
  elseif ($$2 isin "mode") {
    %tokenPos = 3
  }
  elseif ($$2 isin "language") {
    %tokenPos = 4
  }

  if (%tokenPos == 0) {
    echo -tsg tokenPos not found in changeTokenValue
    return 0
  }

  %tokens = $hget(cases, %caseNumber)
  %oldValue = $gettok(%tokens,%tokenPos,36)
  %tokens = $puttok(%tokens,%newValue,%tokenPos,36)

  /hadd -m10 cases %caseNumber %tokens
  /echo -tsg $$2 changed for case %caseNumber : %oldValue => %newValue
}

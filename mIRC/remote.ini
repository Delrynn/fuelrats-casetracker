on 1:EXIT: {
  set -g %translationsLoaded $false
  set -g %caseTracker $false
}

on 1:TEXT:*:#fuelrats,#ratchat: {
  if ($nick isin "MechaSqueak[BOT]") {
    if ($regex(signal, $1-, /RATSIGNAL Case #(\d+) (PC|Xbox|Playstation|unknown platform)(?: \(Code Red\))?(?: \((Odyssey)\))? – CMDR (.+) – System: ".*" \(.+\) – Language: .+ \(([a-z]{2})(?:-\w{2,3})?\)(?: – Nick: ([\w\[\]\^-{|}]+))? (?:\((?:PC|XB|PS)_SIGNAL\))?/SAF)) {
      if ($regml(signal, 6) == $null) {
        /set %nickname $regml(signal, 4)
      }
      else {
        /set %nickname $regml(signal, 6)
      }

      if ($regml(signal, 3) == $null) {
        /set %ody Horizons
      }
      else {
        /set %ody $regml(signal, 3)
      }

      /set -g %caseTracker $true

      /set %caseNumber $regml(signal, 1)
      /set %platform $regml(signal, 2)
      /set %language $regml(signal, 5)

      %tokens = ""
      %tokens = $addtok(%tokens,%nickname,36)
      %tokens = $addtok(%tokens,%platform,36)
      %tokens = $addtok(%tokens,%ody,36)
      %tokens = $addtok(%tokens,%language,36)

      /echo -tsg new client %caseNumber => $gettok(%tokens,1,36) - speaking $gettok(%tokens,4,36) on $gettok(%tokens,2,36) ( $+ $gettok(%tokens,3,36) $+ )

      /hadd -m10 cases %caseNumber %tokens
    }

    if ($regex(nickCMD, $1-, /Client nick for case #(\d+) \(.*\) has been changed to (.*)\./S)) {
      /changeTokenValue nickCMD 2 nickname
    }

    if ($regex(nickChange, $1-, /Caution: Client of case #(\d+) \(.*\) has changed IRC nick to (.*)/S)) {
      /changeTokenValue nickChange 2 nickname
    }

    if ($regex(nickChangeJoin, $1-, /Caution: Case #(\d+) client \(.*\) has rejoined with a different name! \((.*)\)/S)) {
      /changeTokenValue nickChangeJoin 2 nickname
    }

    if ($regex(langChange, $1-, /Language for case #(\d+) \(.*\) has now been changed to (\w{2}).*/S)) {
      /changeTokenValue langChange 2 language
    }

    if ($regex(platformChange, $1-, /Platform for case #(\d+) \(.*\) set to: (PC|Xbox|Playstation)\./S)) {
      /changeTokenValue platformChange 2 platform
    }

    if ($regex(odyChange, $1-, /Case #(\d+) \(.*\) is marked as (not )?using Odyssey/SF)) {
      /changeTokenValue odyChange 2 ody
    }

    if ($regex(close, $1-, /Successfully closed case #(\d+).*/S)) {
      /set %caseNumber $regml(close, 1)

      if ($hget(cases, %caseNumber) != $null) {
        /echo -tsg case %caseNumber closed
        /hdel cases %caseNumber
      }
    }

    if ($regex(md, $1-, /Successfully added case #(\d+) \(.*\) to the deletion list\./S)) {
      /set %caseNumber $regml(md, 1)

      if ($hget(cases, %caseNumber) != $null) {
        /echo -tsg case %caseNumber MDed
        /hdel cases %caseNumber
      }
    }
  }
}
